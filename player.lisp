(in-package #:org.shirakumo.fraf.vpetjam)

(define-shader-entity player (game-entity)
  ((name :initform 'player))
  (:default-initargs :sprite-data (asset 'vpetjam 'player)))

(defmethod handle ((ev tick) (player player))
  (call-next-method)
  (let ((vel (velocity player))
        (dt (dt ev))
        (spd 6)
        (acc 15)
        (dcc 25))
    (cond ((retained 'left)
           (if (<= (vx vel) (- spd))
               (setf (vx vel) (- spd))
               (decf (vx vel) (* dt acc))))
          ((retained 'right)
           (if (<= spd (vx vel))
               (setf (vx vel) spd)
               (incf (vx vel) (* dt acc))))
          (T
           (if (<= (abs (vx vel)) (* dcc dt))
               (setf (vx vel) 0.0)
               (decf (vx vel) (float-sign (vx vel) (* dcc dt))))))
    (cond ((retained 'down)
           (if (<= (vy vel) (- spd))
               (setf (vy vel) (- spd))
               (decf (vy vel) (* dt acc))))
          ((retained 'up)
           (if (<= spd (vy vel))
               (setf (vy vel) spd)
               (incf (vy vel) (* dt acc))))
          (T
           (if (<= (abs (vy vel)) (* dcc dt))
               (setf (vy vel) 0.0)
               (decf (vy vel) (float-sign (vy vel) (* dcc dt))))))
    (nv+ (frame-velocity player) vel)))

(defmethod handle :after ((ev tick) (player player))
  (let ((vel (velocity player)))
    (cond ((or (/= 0 (vx vel)) (/= 0 (vy vel)))
           (cond ((< (abs (vy vel)) (abs (vx vel)))
                  (setf (animation player) 'side)
                  (vsetf (direction player) (float-sign (vx vel)) 0.0))
                 ((< (vy vel) 0)
                  (setf (animation player) 'down)
                  (vsetf (direction player) 0.0 -1.0))
                 ((< 0 (vy vel))
                  (setf (animation player) 'up)
                  (vsetf (direction player) 0.0 +1.0))))
          ((< 0 (vy (direction player)))
           (setf (animation player) 'up-stand))
          ((< (vy (direction player)) 0)
           (setf (animation player) 'down-stand))
          (T
           (setf (animation player) 'side-stand)))))
